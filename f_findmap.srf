$PBExportHeader$f_findmap.srf
$PBExportComments$[GFISCC] Find map file function
global type f_findmap from function_object
end type

forward prototypes
global function string f_findmap ()
end prototypes

global function string f_findmap ();//////////////////////////////////////////////////////////////
//
// Function string f_findmap()
//
// Description: get map file name in the following search order:
//					 1 - registry setting
//					 2 - GFI base directory (gfisccmap.*)
//					 3 - GFI cnf directory (gfisccmap.*)
//					 4 - GFI bin directory (gfisccmap.*)
//
//					 supported image file types are: gif, jpg/jpeg, and bmp
//
// Return: map file name or empty if not found
//
//////////////////////////////////////////////////////////////
string	ls_file, ls_dir[], ls_ext[]={"gif","jpg","jpeg","bmp"}
int		li, li_ext
long		ll[]

if RegistryGet(gnv_syscon.of_getregkey()+"\Global","Map",RegString!,ls_file)>0 then
	ls_file=Trim(ls_file)
	if ls_file<>"" then
		if f_getimageinfo(ls_file,ll) then RETURN ls_file
		RETURN ""
	end if
end if

ls_dir[1]=gnv_syscon.of_getgfidir()
ls_dir[2]=ls_dir[1]+"cnf\"
ls_dir[3]=ls_dir[1]+"bin\"

for li=1 to UpperBound(ls_dir)
	for li_ext=1 to UpperBound(ls_ext)
		ls_file=ls_dir[li]+"gfisccmap."+ls_ext[li_ext]
		if FileExists(ls_file) then
			if f_getimageinfo(ls_file,ll) then RETURN ls_file
			RETURN ""
		end if
	next
next

Message.StringParm="The transit map file was not found. "+gnv_syscon.is_suffix
RETURN ""

end function

